def javaProjects = [project(':IntegrationTests'),
                    project(':ToDoService-Service'),
                    project(':ToDoService-MediumTests'),
                    project(':UserService-Common'),
                    project(':UserService-Service'),
                    project(':UserService-MediumTests'),
                    project(':AccountService-Service'),
                    project(':AccountService-MediumTests'),
                    project(':Eureka-Server')]

def bootProjects = [project(':ToDoService-Service'),
                    project(':UserService-Service'),
                    project(':AccountService-Service'),
                    project(':Eureka-Server')]

def servicesProjects = [project(':ToDoService-Service'),
                        project(':UserService-Service'),
                        project(':AccountService-Service')]


def mediumTestProjects = [project(':ToDoService-MediumTests'),
                          project(':UserService-MediumTests'),
                          project(':AccountService-MediumTests')]




buildscript {
    ext {
        springBootVersion = '1.3.2.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

configure(allprojects) {
    group = 'ua.com.mcgray'
    version = '1.0-SNAPSHOT'


}


configure(javaProjects) {
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'groovy'


    ext.springVersion = "4.2.4.RELEASE"
    ext.springBootVersion = "1.3.2.RELEASE"
    ext.springDataJpaVersion = "1.9.2.RELEASE"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        useJUnit {
            excludeCategories('ua.com.mcgray.test.MediumTest', 'ua.com.mcgray.test.LargeTest')
        }
        testLogging.showStandardStreams = true

    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-starter-parent:1.0.0.RELEASE'
            mavenBom 'org.springframework.cloud:spring-cloud-netflix:1.1.0.BUILD-SNAPSHOT'
        }
    }

    repositories {
        repositories {
            mavenCentral()
            mavenLocal()
            maven { url "http://repo.spring.io/snapshot" }
            maven { url "http://repo.spring.io/milestone" }
            maven { url "http://files.couchbase.com/maven2/" }
            maven { url "http://caucho.com/m2/" }
        }
    }

    dependencies {
        compile 'org.springframework.cloud:spring-cloud-starter-eureka'
        compile 'org.springframework.cloud:spring-cloud-starter-feign'

        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'org.easytesting', name: 'fest-assert', version: '1.4'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
        testCompile 'org.codehaus.groovy:groovy-all:2.3.3'
    }

}

configure(bootProjects) {
    apply plugin: 'spring-boot'

    jar {
        baseName = project.name
    }

    ext {
        querydslVersion    = "3.7.1"
    }

    configurations {
        providedRuntime

    }

    dependencies {

        compile("org.springframework:spring-webmvc:${springVersion}")
        compile("org.springframework:spring-core:${springVersion}")
        compile("org.springframework:spring-web:${springVersion}")
        compile("org.springframework:spring-aop:${springVersion}")
        compile("org.springframework:spring-aspects:${springVersion}")
        compile("org.springframework:spring-core:${springVersion}")
        compile("org.springframework:spring-context:${springVersion}")
        compile("org.springframework:spring-framework-bom:${springVersion}")

        compile("org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}")
        compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
        compile("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")
        compile("org.springframework.boot:spring-boot-starter-aop:${springBootVersion}")

        compile("org.springframework.boot:spring-boot-docs:${springBootVersion}")
        compile("org.springframework.data:spring-data-jpa:${springDataJpaVersion}")

        compile('com.google.code.gson:gson:2.3')
        testCompile("com.h2database:h2:1.3.148")
        testCompile("com.jayway.jsonpath:json-path:0.9.1")
        testCompile("org.springframework:spring-test:${springVersion}")

        compile('org.liquibase:liquibase-core:3.3.0')

        compile("commons-dbcp:commons-dbcp:1.2.2")
        compile('com.caucho:resin-hessian:4.0.23')

        compile("org.apache.commons:commons-lang3:3.2.1")
        compile("org.apache.commons:commons-collections4:4.0")
        compile("org.apache.tomcat:tomcat-jdbc:7.0.54")
        compile("mysql:mysql-connector-java")

        testCompile ("org.unitils:unitils-dbunit:3.4.2") {
            exclude group: 'org.hibernate'
        }

        providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    }
}

configure(servicesProjects) {
    dependencies {
        compile project(":UserService-Common")
    }
}

configure(mediumTestProjects) {
    task mediumTests(dependsOn: test, type: Test) {
        useJUnit {
            includeCategories('ua.com.mcgray.test.MediumTest')
        }
        testLogging.showStandardStreams = true

        testLogging {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }

    dependencies {

        compile project(':UserService-Common')

        testCompile("org.springframework:spring-test:${springVersion}")
        testCompile("com.jayway.jsonpath:json-path:0.9.1")
    }
}

task wrapper(type: Wrapper) {
    description = "Generates gradlew[.bat] scripts"
    gradleVersion = "2.10"
}

