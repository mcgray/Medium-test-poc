ext {
    generatedSourcesDir = file("${buildDir}/generated-sources")
    querydslVersion    = "3.7.1"
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir generatedSourcesDir
        }
    }
}

configurations {
    querydslapt
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = generatedSourcesDir
}
compileJava {
    doFirst {
        generatedSourcesDir.mkdirs();
    }
    options.compilerArgs += ['-s', generatedSourcesDir]

    dependsOn generateQueryDSL
}


dependencies {

    compile('javax.validation:validation-api:1.1.0.Final')

    compile('org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final')
    compile('org.hibernate:hibernate-validator:5.0.1.Final')

    compile("org.springframework:spring-core:${springVersion}")
    compile("org.springframework:spring-context:${springVersion}")
    compile("org.springframework:spring-aop:${springVersion}")
    compile("org.springframework:spring-aspects:${springVersion}")
    compile("org.springframework:spring-webmvc:${springVersion}")
    compile("org.springframework:spring-web:${springVersion}")
    compile("org.springframework:spring-orm:${springVersion}")

    compile("org.springframework.data:spring-data-jpa:${springDataJpaVersion}")
    compile("com.mysema.querydsl:querydsl-core:${querydslVersion}")
    compile("com.mysema.querydsl:querydsl-jpa:${querydslVersion}")
    querydslapt  "com.mysema.querydsl:querydsl-apt:${querydslVersion}"

    compile("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")

    compile('mysql:mysql-connector-mxj:5.0.12')
    compile('mysql:mysql-connector-mxj-db-files:5.0.12')
    compile('commons-io:commons-io:2.4')
    compile('org.apache.mina:mina-core:1.1.6')

    compile("org.aspectj:aspectjrt:1.8.1")
    compile("org.aspectj:aspectjweaver:1.8.1")


    compile('commons-lang:commons-lang:2.6')

    compile("org.apache.tomcat:tomcat-jdbc:7.0.54")

}